plugins {
    id 'java-library'
    id 'application'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'com.palantir.docker' version '0.13.0'
    id "org.unbroken-dome.test-sets" version "1.4.2"
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

mainClassName = 'com.sparkcentral.demoscmessenger.Application'

ext {
    buildNumber = System.properties['buildNumber']
}

repositories {
    jcenter()
    maven { url 'http://repo.spring.io/milestone' }
}

run {
    jvmArgs  '-Dspring.profiles.active=local'
}

testSets {
    integrationTest
}

check.dependsOn(integrationTest)
integrationTest.mustRunAfter test


dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.M8'
        mavenBom 'io.spring.platform:platform-bom:Cairo-RC1'
    }

    dependencies {
        dependency 'io.jsonwebtoken:jjwt:0.8.0'
        dependency 'javax.xml.bind:jaxb-api:2.3.0'
        dependency 'net.logstash.logback:logstash-logback-encoder:4.11'
    }
}

dependencies {
    implementation(
            'javax.xml.bind:jaxb-api',
            'org.springframework.boot:spring-boot-starter-webflux',
            'org.springframework.boot:spring-boot-starter-actuator',
            'io.jsonwebtoken:jjwt',
            'net.logstash.logback:logstash-logback-encoder',
            'org.springframework.cloud:spring-cloud-starter-sleuth'
    )

    testImplementation(
            'junit:junit',
            'org.assertj:assertj-core',
            'org.mockito:mockito-core'
    )

    integrationTestImplementation(
            'org.springframework.boot:spring-boot-starter-test'
    )

}

docker {
    name "sparkcentral/sparkcentral-messenger-demoapp:${System.properties['buildNumber']}"
    tags 'latest'
    files tasks.distTar.outputs
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}
